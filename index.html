<head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <link rel="stylesheet" href="css/style.css">

    <script src="lib/glMatrix-0.9.5.min.js"></script>
    <script type='text/javascript' src='./lib/webgl-obj-loader.js'></script>

    <!-- for skybox -->
    <script type="text/javascript" src="./js/webgl-utils.js"></script>

    <!-- jquery -->
    <script src="./lib/jquery.js"></script>

    <!-- MIDI -->
    <script src="./lib/jasmid/replayer.js"></script>
    <script src="./lib/jasmid/midifile.js"></script>
    <script src="./lib/jasmid/stream.js"></script>

    <script src="./lib/midi/MIDI/MIDI.js"></script>
    <script src="./lib/midi/Window/DOMLoader.script.js"></script>

    <script src="./lib/midi/inc/Base64.js"></script>
    <script src="./lib/midi/inc/base64binary.js"></script>

    <!-- Own sources -->
    <script src="./js/midi.js"></script>
    <script src="js/core.js" type="text/javascript"></script>
    
    <!-- SKYBOX -->
    <script id="skyboxVertexShader" type="x-shader/x-vertex">
        uniform mat4 projection;
        uniform mat4 modelview;
        attribute vec3 coords;
        varying vec3 vCoords;

        void main() {
            vec4 eyeCoords = modelview * vec4(coords,1.0);
            gl_Position = projection * eyeCoords;
            vCoords = coords;//gl_Position.xyz;
        }
    </script>

    <script id="skyboxFragmentShader" type="x-shader/x-fragment">
        precision mediump float;
        varying vec3 vCoords;
        uniform samplerCube skybox;

        void main() {
            gl_FragColor = textureCube(skybox, vCoords);
        }
    </script>

    <!-- TUNNEL -->
    <script id="tunnel-shader-vs" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;
        attribute vec2 aTextureCoord;

        uniform mat4 uMVMatrix; // model-view matrix
        uniform mat4 uPMatrix;  // projection matrix
        uniform mat3 uNMatrix;  // normal matrix

        varying vec2 vTextureCoord;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;

        void main(void) {
            vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
            gl_Position = uPMatrix * vPosition;
            vTextureCoord = aTextureCoord;
            vTransformedNormal = uNMatrix * aVertexNormal;
        }
    </script>

    <script id="tunnel-shader-fs" type="x-shader/x-fragment">
        precision mediump float;

        varying vec2 vTextureCoord;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;

        uniform vec3 uMaterialColor;
        uniform float uMaterialAlpha;

        uniform vec3 uPointLightingLocation;            // light direction uniform
        uniform vec3 uAmbientColor;
        uniform vec3 uDiffuseColor;        // difuse light color
        uniform vec3 uSpecularColor;       // specular light color
        uniform float uShininess;

        void main(void) {
            vec3 lightWeight;

            vec3 lightDirection = normalize(uPointLightingLocation - vPosition.xyz);
            vec3 normal = normalize(vTransformedNormal);

            // diffuese component
            float diffuseLightWeight = max(dot(normal, lightDirection), 0.0);

            // Specular component
            float specularLightWeight = 0.0;
            vec3 eyeDirection = normalize(-vPosition.xyz);
            vec3 reflectionDirection = reflect(-lightDirection, normal);
            specularLightWeight = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uShininess);
            
            lightWeight = uAmbientColor
                + uSpecularColor * specularLightWeight
                + uDiffuseColor * diffuseLightWeight;

            gl_FragColor = vec4(uMaterialColor * lightWeight, uMaterialAlpha);
            }
    </script>

    <!-- BALLS -->
    <script id="shader-vs-balls" type="x-shader/x-vertex">
        attribute vec2 aVertexPosition;

        uniform float uAspectRatio;

        varying vec2 vPos;

        void main(void) {
            vPos = aVertexPosition;
            vPos.x *= uAspectRatio;
            gl_Position = vec4(aVertexPosition, 0.0, 1.0);
        }
    </script>

    <script id="shader-fs-balls" type="x-shader/x-fragment">
        // inspired by https://www.shadertoy.com/view/MtXSzS

        precision mediump float;

        uniform float uTime;
        uniform vec3 uCamera;
        uniform vec3 uCenter;
        uniform float uScale;
        uniform float uNoiseFrequency;

        varying vec2 vPos;

        #define saturate(oo) clamp(oo, 0.0, 1.0)

        #define MARCH_STEPS 8
        #define RADIUS 2.0
        #define NOISE_AMP 0.06
        #define ANIMATION vec3(0.0, -3.0, 0.5)
        #define C1 vec4(0.0, 0.0, 0.0, 0.9)
        #define C2 vec4(0.6, 0.2, 0.8, 0.9)
        #define C3 vec4(1.0, 0.55, 0.0, 0.9)
        #define C4 vec4(1.0, 1.0, 0.5, 0.9)

        vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
        vec4 taylorInvSqrt(vec4 r){ return 1.79284291400159 - 0.85373472095314 * r; }

        float snoise(vec3 v) {
            const vec2 C = vec2(1.0/6.0, 1.0/3.0);
            const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);
            // First corner
            vec3 i  = floor(v + dot(v, C.yyy));
            vec3 x0 = v - i + dot(i, C.xxx);
            // Other corners
            vec3 g = step(x0.yzx, x0.xyz);
            vec3 l = 1.0 - g;
            vec3 i1 = min(g.xyz, l.zxy);
            vec3 i2 = max(g.xyz, l.zxy);
            vec3 x1 = x0 - i1 + C.xxx;
            vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
            vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y
            // Permutations
            i = mod289(i);
            vec4 p = permute( permute( permute( i.z + vec4(0.0, i1.z, i2.z, 1.0)) + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
            // Gradients: 7x7 points over a square, mapped onto an octahedron.
            // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
            float n_ = 0.142857142857; // 1.0/7.0
            vec3  ns = n_ * D.wyz - D.xzx;
            vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)
            vec4 x_ = floor(j * ns.z);
            vec4 y_ = floor(j - 7.0 * x_);    // mod(j,N)
            vec4 x = x_ *ns.x + ns.yyyy;
            vec4 y = y_ *ns.x + ns.yyyy;
            vec4 h = 1.0 - abs(x) - abs(y);
            vec4 b0 = vec4(x.xy, y.xy);
            vec4 b1 = vec4(x.zw, y.zw);
            vec4 s0 = floor(b0) * 2.0 + 1.0;
            vec4 s1 = floor(b1) * 2.0 + 1.0;
            vec4 sh = -step(h, vec4(0.0));
            vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
            vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;
            vec3 p0 = vec3(a0.xy, h.x);
            vec3 p1 = vec3(a0.zw, h.y);
            vec3 p2 = vec3(a1.xy, h.z);
            vec3 p3 = vec3(a1.zw, h.w);
            //Normalise gradients
            vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
            p0 *= norm.x;
            p1 *= norm.y;
            p2 *= norm.z;
            p3 *= norm.w;
            // Mix final noise value
            vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
            m = m * m;
            return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3)));
        }

        float turbulence(vec3 position, float minFreq, float maxFreq, float qWidth) {
            float cutoff = clamp(0.5 / qWidth, 0.0, maxFreq);
            float fade = clamp(2.0 * (cutoff - minFreq) / cutoff, 0.0, 1.0);
            float value = fade * abs(snoise(position * minFreq)) / minFreq;
            return 1.0 - value;
        }

        float sphereDist(vec3 position, vec3 c, float r) {
            return length(position - c) - r;
        }

        vec4 shade(float distance) {
            float c1 = saturate(distance * 5.0 + 0.5);
            float c2 = saturate(distance * 5.0);
            float c3 = saturate(distance * 3.4 - 0.5);
            vec4 a = mix(C1, C2, c1);
            vec4 b = mix(a,  C3, c2);
            return   mix(b,  C4, c3);
        }

        float renderBall(vec3 position, vec3 c, float r, out float distance) {
            float noise = turbulence(position * uNoiseFrequency + ANIMATION * uTime, 0.1, 1.5, 0.03) * NOISE_AMP;
            noise = saturate(abs(noise));
            distance = sphereDist(position, c, r) - noise;
            return noise;
        }

        // Basic ray marching method.
        vec4 march(vec3 rayOrigin, vec3 rayStep, vec3 c, float r) {
            vec3 position = rayOrigin;
            float distance;
            float displacement;
            for (int step = MARCH_STEPS; step >=0  ; --step) {
                displacement = renderBall(position, c, r, distance);
                if (distance < 0.05) break;
                position += rayStep * distance;
            }
            return mix(shade(displacement), vec4(0.0), float(distance >= 0.5));
        }

        bool intersectSphere(vec3 ro, vec3 rd, vec3 pos, float radius, out vec3 intersectPoint) {
            vec3 relDistance = (ro - pos);
            float b = dot(relDistance, rd);
            float c = dot(relDistance, relDistance) - radius*radius;
            float d = b*b - c;
            intersectPoint = ro + rd*(-b - sqrt(d));
            return d >= 0.0;
        }

        void main(void) {                                
            vec3 ww = normalize(vec3(0.0, 0.0, 0.0) - uCamera);
            vec3 uu = normalize(cross(vec3(0.0, 1.0, 0.0), ww));
            vec3 vv = normalize(cross(ww, uu));
            vec3 direction = normalize(vPos.x * uu + vPos.y * vv + ww);
            
            vec4 col = vec4(0.0);
            vec3 intersection;

            if (intersectSphere(uCamera, direction, uCenter, uScale * RADIUS + NOISE_AMP * 6.0, intersection)) {
                col = march(intersection, direction, uCenter, uScale * RADIUS);
            }

            gl_FragColor = col;
        }
    </script>

</head>

<body onkeydown="mapKey(event, true)" onkeyup="mapKey(event, false)">
    <h1>interactive music visualization</h1>
    <div class="right">
        <span>
            <label>Load MIDI file:</label>
            <input type="file" id="file_upload" class="upload"/>
        </span>

        <p id="midiFail"></p>

        <button onclick="lowNote()" class="button">Send minimal available MIDI note</button><span id="midiLow" class="noMIDI">min = 0</span>
        <button onclick="highNote()" class="button">Send maximal available MIDI note</button><span id="midiHigh" class="noMIDI">max = 127</span>
        <label style="margin-top: 50px">MIDI on computer keyboard:</label>
        <img src="assets/img/keyboardMIDImapping.png" height="15%">
    </div>

    <!-- <canvas id="glcanvas" width="1280px" height="720px"> -->
    <canvas id="glcanvas" width="1024px" height="576px">
        No <code>&lt;canvas&gt;</code> suppport in your browser.
    </canvas>

</body>
